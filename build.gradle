plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

group 'seedu.duke'
version '0.1.5'

repositories {
    mavenCentral()
}

dependencies {
    for (module in ['base', 'controls', 'fxml', 'graphics'])
        for (os in ['win', 'mac', 'linux'])
            implementation group: 'org.openjfx', name: 'javafx-' + module, version: '11.0.2', classifier: os

    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.0'
}

application {
    // Change this to your main class.
    mainClassName = "duke.Launcher"
}

tasks.withType(JavaCompile) {
    sourceCompatibility = '11'
    targetCompatibility = '11'
    options.encoding = 'UTF-8'
}

checkstyle {
    toolVersion = '8.23'
}

shadowJar {
    archiveBaseName.set "duke"
    archiveVersion.set "0.1.5"
    archiveClassifier.set null
    archiveAppendix.set null
}

run {
    standardInput = System.in
}

test {
    useJUnitPlatform()
}
