plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'com.github.spotbugs' version "2.0.0"
}

group 'seedu.duke'
version '0.2.2'

repositories {
    mavenCentral()
}

application {
    // Change this to your main class.
    mainClassName = 'duke.Launcher'
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.0'

    String javaFxVersion = '11'
    implementation group: 'org.openjfx', name: 'javafx-base',     version: javaFxVersion, classifier: 'win'
    implementation group: 'org.openjfx', name: 'javafx-base',     version: javaFxVersion, classifier: 'mac'
    implementation group: 'org.openjfx', name: 'javafx-base',     version: javaFxVersion, classifier: 'linux'
    implementation group: 'org.openjfx', name: 'javafx-controls', version: javaFxVersion, classifier: 'win'
    implementation group: 'org.openjfx', name: 'javafx-controls', version: javaFxVersion, classifier: 'mac'
    implementation group: 'org.openjfx', name: 'javafx-controls', version: javaFxVersion, classifier: 'linux'
    implementation group: 'org.openjfx', name: 'javafx-fxml',     version: javaFxVersion, classifier: 'win'
    implementation group: 'org.openjfx', name: 'javafx-fxml',     version: javaFxVersion, classifier: 'mac'
    implementation group: 'org.openjfx', name: 'javafx-fxml',     version: javaFxVersion, classifier: 'linux'
    implementation group: 'org.openjfx', name: 'javafx-graphics', version: javaFxVersion, classifier: 'win'
    implementation group: 'org.openjfx', name: 'javafx-graphics', version: javaFxVersion, classifier: 'mac'
    implementation group: 'org.openjfx', name: 'javafx-graphics', version: javaFxVersion, classifier: 'linux'
}

checkstyle {
    toolVersion = '8.23'
}

spotbugs {
    excludeFilter = file("$projectDir/config/spotbugs/excludeFilter.xml")
}

test {
    useJUnitPlatform()
}

task runCli(type: JavaExec, dependsOn: classes) {
    main = 'duke.Launcher'
    description = 'Run Duke CLI'
    standardInput = System.in
    classpath = sourceSets.main.runtimeClasspath
    args = ['cli']
}

task runGui(type: JavaExec, dependsOn: classes) {
    main = 'duke.Launcher'
    description = 'Run Duke GUI'
    classpath = sourceSets.main.runtimeClasspath
    args = ['gui']
}

shadowJar {
    archiveBaseName = 'duke'
    archiveVersion = '0.2.2'
    archiveClassifier = null
    archiveAppendix = null
}

task textUiTest(type: Exec) {
    description = 'Runs text UI tests'
    group = 'verification'
    shouldRunAfter test

    workingDir "$projectDir/text-ui-test"
    commandLine 'bash', '-c', './runtest.sh'
}

test.dependsOn textUiTest
